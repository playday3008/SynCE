AC_PREREQ(2.52)
AC_INIT(synce-hal, @@VERSION@@)
AC_CONFIG_HEADERS(config.h)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE

AM_MAINTAINER_MODE
AC_PROG_MAKE_SET

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

dnl Check for header files.
AC_STDC_HEADERS

dnl Check for Synce
PKG_CHECK_MODULES(SYNCE, [libsynce >= 0.10.0])

dnl Check for GLib
PKG_CHECK_MODULES(GLIB, [glib-2.0 >= 2.7, gobject-2.0 >= 2.4])

dnl Check for GNet
PKG_CHECK_MODULES(GNET, gnet-2.0)

dnl Check for D-Bus
PKG_CHECK_MODULES(DBUS, [dbus-1 >= 0.60, dbus-glib-1 >= 0.60])

dnl Check for earlier version of dbus-glib
PKG_CHECK_MODULES(DBUSGLIB074, [dbus-glib-1 >= 0.74], [early_dbusglib=no], [early_dbusglib=yes])
if test "x$early_dbusglib" = "xyes"; then
   AC_DEFINE_UNQUOTED([EARLY_DBUSGLIB], [1], [dbus-glib older than 0.74])
fi

dnl Check for HAL
PKG_CHECK_MODULES(HAL, hal)

dnl Check for override of default hal addon dir
AC_MSG_CHECKING([hal addon dir])
AC_ARG_WITH(hal-addon-dir, [  --with-hal-addon-dir=PATH     Location of the hal addon scripts.],
            with_hal_addon_dir="$withval", with_hal_addon_dir="no")
if test "x${with_hal_addon_dir}" = "xno"; then
    hal_addon_dir="`pkg-config --variable=libdir hal`/hal"

    hald_test_output=`hald --daemon=no 2>&1 | grep 'allowed paths' | grep "$hal_addon_dir"`
    if test $? -ne 0
    then
        AC_MSG_ERROR([Automatically determined hal script location ${hal_addon_dir} not in allowed paths, use --with-hal-addon-dir])
    fi
    AC_MSG_RESULT([$hal_addon_dir])
else
    hal_addon_dir="$with_hal_addon_dir"

    hald_test_output=`hald --daemon=no 2>&1 | grep 'allowed paths' | grep "$hal_addon_dir"`
    if test $? -ne 0
    then
        AC_MSG_WARN([Specified hal script location ${hal_addon_dir} not in allowed paths])
    else
        AC_MSG_RESULT([$hal_addon_dir])
    fi
fi
AC_SUBST(hal_addon_dir)


AC_ARG_ENABLE(bluetooth-support, AC_HELP_STRING([--enable-bluetooth-support], [Build in bluetooth support]),enable_bluetooth_support="$enableval",enable_bluetooth_support=no)
AM_CONDITIONAL(ENABLE_BLUETOOTH_SUPPORT, test "x$enable_bluetooth_support" = "xyes")
if test "x$enable_bluetooth_support" = "xyes"; then
       AC_DEFINE(ENABLE_BLUETOOTH_SUPPORT, 1, [Building in bluetooth support])
fi


AS_AC_EXPAND(LIBEXECDIR, "$libexecdir")
AS_AC_EXPAND(SYSCONFDIR, "$sysconfdir")
AS_AC_EXPAND(LOCALSTATEDIR, "$localstatedir")
AS_AC_EXPAND(DATADIR, "$datadir")

AC_CONFIG_FILES([
        hal/hal-synce-rndis \
        hal/hal-synce-serial \
        bluetooth/hal-synce-bluetooth \
        bluetooth/synce-bt-ipup \
        bluetooth/synce-bt-ipdown \
        etc/synce-hal.conf] )

AC_OUTPUT([
	Makefile
	hal/Makefile
	src/Makefile
	etc/Makefile
	bluetooth/Makefile
])
