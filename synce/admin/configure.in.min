# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.

AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(@MODULENAME@, @VERSION@) dnl searches for some needed programs

dnl KDE_SET_PREFIX

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL

dnl Test -fshort-wchar
KDE_CHECK_COMPILER_FLAG(fshort-wchar,
[ SHORT_WCHAR_FLAGS="-fshort-wchar" ],
[ SHORT_WCHAR_FLAGS="-U__WCHAR_TYPE__ -D__WCHAR_TYPE__=short\ unsigned\ int" ] )
AC_SUBST(SHORT_WCHAR_FLAGS)

dnl Test for libiconv on OpenBSD
AC_MSG_CHECKING([for path to iconv library])
AC_ARG_WITH(iconv,
[  --with-iconv=DIR        Use separate library for iconv functions.
                          DIR is the prefix to where iconv is installed],
[
  if test "$withval" != "no"; then
  	if test "$withval" != "yes"; then
			AC_MSG_RESULT([$withval])
			LDFLAGS="$LDFLAGS -L$withval/lib"
			CPPFLAGS="$CPPFLAGS -I$withval/include"
		else
			AC_MSG_RESULT([(use default)])
   	fi
  else
	AC_MSG_RESULT([no])
  fi
],[
	AC_MSG_RESULT([(use default)])
])
AC_MSG_CHECKING([for iconv_open()])
LIBICONV=""
dnl evil iconv.h redefines iconv_open() as a macro
AC_TRY_LINK([#include <iconv.h>],[iconv_open(0,0);],[
	AC_MSG_RESULT([yes])
],[
	AC_MSG_RESULT([no])
	AC_MSG_CHECKING([for iconv_open() with -liconv])
	LIBS_save="$LIBS"
	LIBS="$LIBS -liconv"
	AC_TRY_LINK([#include <iconv.h>],[iconv_open(0,0);],[
		AC_MSG_RESULT([yes])
		LIBICONV="-liconv"
	],[
		AC_MSG_RESULT([no])
	])
	LIBS="$LIBS_save"
])
AC_SUBST(LIBICONV)

dnl This will define WORDS_BIGENDIAN if we run on a big endian machine
AC_C_BIGENDIAN()

dnl This will test for endian-swapping headers
AC_CHECK_HEADERS(byteswap.h sys/byteswap.h endian.h machine/endian.h sys/types.h)

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
dnl AM_KDE_WITH_NLS

dnl AC_PATH_KDE replaced by the lines below
all_libraries="$all_libraries $USER_LDFLAGS"
all_includes="$all_includes $USER_INCLUDES"
AC_SUBST(all_includes)
AC_SUBST(all_libraries)

dnl Perform program name transformation
AC_ARG_PROGRAM

