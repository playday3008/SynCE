AC_DEFUN([SYNCE_CHECK_DYNAMITE],
	[
		if test x = x$dynamite_checked; then
			synce_kde_save_cflags="$CPPFLAGS"
			synce_kde_save_ldflags="$LDFLAGS"

			AC_ARG_WITH(libdynamite, 
				AC_HELP_STRING(
					[--with-libdynamite=DIR],
					[Search for libdynamite in DIR/include and DIR/lib]
				),
				[
					DYNAMITE_INCLUDES="-I${withval}/include"
					DYNAMITE_LDFLAGS="-L${withval}/lib"
				]
			)

			AC_ARG_WITH(libdynamite-include,
				AC_HELP_STRING(
					[--with-libdynamite-include=DIR],
					[Search for libdynamite header files in DIR]
				),
				[
					DYNAMITE_INCLUDES="-I${withval}"
				]
			)

			AC_ARG_WITH(libdynamite-lib,
				AC_HELP_STRING(
					[--with-libdynamite-lib=DIR],
					[Search for libdynamite library files in DIR]
				),
				[
					DYNAMITE_LDFLAGS="-L${withval}"
				]
			)

			LDFLAGS="$LDFLAGS $SYNCE_LDFLAGS"
			AC_CHECK_LIB(dynamite, dynamite_explode,
				[
					DYNAMITE_LIB="-ldynamite"
				],
				[
					AC_MSG_ERROR([Can't find libdynamite library])
					exit
				]
			)

			CPPFLAGS="$CPPFLAGS $DYNAMITE_INCLUDES"
			AC_CHECK_HEADERS(libdynamite.h,,
				[
					AC_MSG_ERROR([Can't find libdynamite.h])
					exit
				]
			)

			AC_SUBST(DYNAMITE_INCLUDES)
			AC_SUBST(DYNAMITE_LDFLAGS)
			AC_SUBST(DYNAMITE_LIB)

			CPPFLAGS="$synce_kde_save_cflags"
			LDFLAGS="$synce_kde_save_ldflags"
			unset synce_kde_save_cflags
			unset synce_kde_save_ldflags
			dynamite_checked=yes
		fi
	]
)
